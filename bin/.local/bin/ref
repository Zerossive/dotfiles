#!/usr/bin/env zsh
# ==============================================================================
# Script Name: ref
# Description: create or view reference notes in obsidian vault
# Usage: ref [<note name>]
# Author: Danny Harris
# Dependencies: fzfc, neovim, fd, bat
# ==============================================================================

# -e: exit on error, -u: treat unset variables as errors, -o pipefail: pipe will fail if any command fails
set -euo pipefail

# set global variables
notes_dir="$HOME/Elysium/Obsidian Vault/Notes/Reference/"

# default fzf options
fzf_opts=(
	--prompt "Select a reference: "
	--preview "printf \"%s\" \"$notes_dir{}.md\" | xargs -I _ bat --color always --style plain _"
	--header 'Delete: ctrl-x    Add New: ctrl-a    Preview: alt-p, alt-u/d    Copy: ctrl-y'
	--height '100%'
	--bind "ctrl-x:execute(printf \"%s\" \"$notes_dir{}.md\" | xargs -I _ rm _)+exclude"
	--bind "ctrl-a:execute(
        CLEANED=\$(sed \"s/^'//; s/'$//\" <<< {q});
        FILE=\"$notes_dir\$CLEANED.md\";
        nvim \"\$FILE\" -c \"norm i# \$CLEANEDgogoG\"
    )+change-query(NULL)+print-query"
	--bind "enter:accept-or-print-query"
)

# exit if notes dir doesn't exist
if [[ ! -d "$notes_dir" ]]; then
	printf "%s\n" "error: directory doesn't exist: $notes_dir" >&2
	exit 1
fi

# if no arguments, list notes
if [ $# -eq 0 ]; then
	# select note to edit or create
	selected_note="$(
		fd --absolute-path --extension md --type f . "$notes_dir" |
			sed "s|^$notes_dir||; s|.md$||" |
			fzfc "${fzf_opts[@]}" || printf ""
	)"

	# exit if no note is selected
	if [[ -z "$selected_note" ]]; then
		printf "%s\n" "note selection cancelled" >&2
		exit 1
	fi

	# exit if selected note is named NULL (for creating new notes within fzf)
	if [[ "$selected_note" == "NULL" ]]; then
		exit 0
	fi

	# create new note if selected note doesn't exist
	if [[ ! -f "$notes_dir/$selected_note.md" ]]; then
		nvim "$notes_dir/$selected_note.md" -c "norm i# $selected_notegogoG"
		exit 0
	fi

	# open selected note
	nvim "$notes_dir/$selected_note.md"
	exit 0
fi

# show help
if [[ "$1" == "-h" || "$1" == "--help" ]]; then
	cat <<-EOF
		Usage: ${0##*/} [<note-name>]
		Create or view reference notes.
	EOF
	exit 0
fi

# create a new note if arguments are provided
nvim "$notes_dir/$*.md" -c "norm i# $*gogoG"

# vim: set ft=sh:
